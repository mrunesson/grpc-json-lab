group 'org.linuxalert.lab'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.10'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt' // Annotation processor
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

def grpcVersion = '1.2.0'


repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    generatedFilesBaseDir = "$projectDir/build/generated"
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }

}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
    }
}

dependencies {
    compileOnly group: 'com.google.auto.value', name: 'auto-value', version: '1.4'
    apt group: 'com.google.auto.value', name: 'auto-value', version: '1.4'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.2.0'

    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    compile  'io.github.openfeign:feign-core:9.4.0'
    compile  'io.github.openfeign:feign-jackson:9.4.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
